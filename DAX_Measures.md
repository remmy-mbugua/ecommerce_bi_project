#  DAX Measures – E-Commerce BI Project

This document contains the full DAX measures used across different pages of the Power BI report. Measures are grouped by the report section where they are used.

---

###  Executive Overview

**MoM Revenue Growth**  
Uses EDATE() to get the previous month, then calculates revenue for both months and compares them. 
IF(ISBLANK(...)) prevents errors when prior data is missing. 
This logic isolates monthly change cleanly without needing full time series data.

```DAX
MoM Revenue Growth = 
VAR SelectedMonth = MAX('public dim_date'[Month Start])
VAR CurrentPeriod = 
    CALCULATE([Revenue], FILTER(ALL('public dim_date'), 'public dim_date'[Month Start] = SelectedMonth))
VAR PreviousPeriod =
    CALCULATE([Revenue], FILTER(ALL('public dim_date'), 'public dim_date'[Month Start] = EDATE(SelectedMonth, -1)))
RETURN IF(ISBLANK(PreviousPeriod), 0, (CurrentPeriod - PreviousPeriod) / PreviousPeriod)
```

**Average Order Value**  
Calculates the average value of an order by dividing total revenue by number of invoices.

```DAX
Average Order Value = 
AVERAGEX(VALUES('public fact_sales'[Invoice No]), CALCULATE(SUM('public fact_sales'[Total Amount])))
```

**Average Basket Size**  
Calculates the average number of items per order for sales transactions.

```DAX
Average Basket Size = 
DIVIDE(
    CALCULATE(COUNTROWS('public fact_sales'), FILTER('public fact_sales', 'public fact_sales'[Transaction Type] = "Sale")),
    CALCULATE(DISTINCTCOUNT('public fact_sales'[Invoice No]), FILTER('public fact_sales', 'public fact_sales'[Transaction Type] = "Sale"))
)
```

**Active Customers This Month**  
Counts distinct customers who made a purchase this month.

```DAX
Active Customers This Month = 
VAR SelectedMonth = MAX('public dim_date'[Month Label])
VAR Result = CALCULATE(DISTINCTCOUNT('public fact_sales'[Customer ID]), 'public dim_date'[Month Label] = SelectedMonth)
RETURN IF(ISBLANK(Result), 0, Result)
```

**Churned Customers**
Calculates customers who made a purchase in the previous month but did not return in the current month.

This uses EXCEPT() between two time windows to define churn as a complete lack of activity in the following month. 
I chose this over simply measuring recency because it gives a clear month-on-month churn signal. 
It doesn’t capture long-term churn, but works well for detecting short-term drop-offs, 
especially after acquisition spikes or seasonal surges.

```DAX
Churned Customers = 
VAR SelectedMonth = MAX('public dim_date'[Date])
VAR CurrentPeriod = 
    CALCULATETABLE(VALUES('public fact_sales'[Customer ID]), FILTER(ALL('public dim_date'), 'public dim_date'[Year] = YEAR(SelectedMonth) && 'public dim_date'[Month] = MONTH(SelectedMonth)), 'public fact_sales'[Transaction Type] = "Sale")
VAR PreviousPeriod =
    CALCULATETABLE(VALUES('public fact_sales'[Customer ID]), FILTER(ALL('public dim_date'), 'public dim_date'[Year] = YEAR(EDATE(SelectedMonth, -1)) && 'public dim_date'[Month] = MONTH(EDATE(SelectedMonth, -1))), 'public fact_sales'[Transaction Type] = "Sale")
RETURN COUNTROWS(EXCEPT(PreviousPeriod, CurrentPeriod))
```


### Product Performance

**Revenue per Product**  
Average revenue per product across filtered dataset.

```DAX
Revenue per Product = 
VAR Result = DIVIDE([Revenue], [Product Count])
RETURN IF(ISBLANK(Result), 0, Result)
```

**Return Rate**  
Percentage of items returned based on quantity.

```DAX
Return Rate = 
VAR Result =
DIVIDE(
    CALCULATE(SUM('public fact_sales'[Quantity]) * -1, 'public fact_sales'[Transaction Type] = "Return"),
    CALCULATE(SUM('public fact_sales'[Quantity]), 'public fact_sales'[Transaction Type] = "Sale")
)
RETURN IF(ISBLANK(Result), 0, Result)
```

**Cumulative Revenue %**  
Shows what % of total revenue is generated by the top products cumulatively (used for Pareto analysis).

Uses RANKX to order products by revenue, then filters all products with rank <= current to compute a running total. 
Dividing by the grand total gives cumulative %. 
Helps visually identify top performers contributing to most revenue (e.g. 80/20 rule).

```DAX
Cumulative Revenue % = 
VAR CurrentRank = 
    RANKX(ALLSELECTED('public dim_product'), [Revenue], , DESC, DENSE)
RETURN
DIVIDE(
    CALCULATE([Revenue], FILTER(ALLSELECTED('public dim_product'), RANKX(ALLSELECTED('public dim_product'), [Revenue], , DESC, DENSE) <= CurrentRank)),
    CALCULATE([Revenue], ALLSELECTED('public dim_product'))
)
```

**Customer Count**  
Simple count of unique customers.

```DAX
Customer Count = 
VAR Result = DISTINCTCOUNT('public dim_customer'[Customer ID])
RETURN IF(ISBLANK(Result), 0, Result)
```


### Market/Region Analysis

**Top Country by Revenue**  
Returns the name of the top revenue-generating country.

```DAX
Top Country by Revenue = 
VAR CountryRevenue =
ADDCOLUMNS(VALUES('public dim_geography'[Country]), "Country Revenue", CALCULATE(SUM('public fact_sales'[Total Amount])))
RETURN SELECTCOLUMNS(TOPN(1, CountryRevenue, [Country Revenue], DESC), "Top Country", [Country])
```

**Revenue per Customer**  
Average revenue per customer across entire dataset.

```DAX
Revenue per Customer = 
DIVIDE(SUM('public fact_sales'[Total Amount]), DISTINCTCOUNT('public fact_sales'[Customer ID]))
```


### Customer Behaviour

**Customer Acquisition**  
Counts net-new customers in the selected month.

Creates two customer sets using CALCULATETABLE() — one for the current month, one for all prior. 
Then uses EXCEPT() to find customers who are new. 
This helps measure the effectiveness of marketing, referrals, or market expansion efforts.

```DAX
Customer Acquisition = 
VAR SelectedDate = MAX('public dim_date'[Date])
VAR StartMonth = DATE(YEAR(SelectedDate), MONTH(SelectedDate), 1)
VAR EndMonth = EOMONTH(SelectedDate, 0)
VAR CurrentPeriod =
    CALCULATETABLE(VALUES('public fact_sales'[Customer ID]), 'public fact_sales'[Transaction Type] = "Sale", FILTER(ALL('public dim_date'), 'public dim_date'[Date] >= StartMonth && 'public dim_date'[Date] <= EndMonth))
VAR PreviousCustomers =
    CALCULATETABLE(VALUES('public fact_sales'[Customer ID]), 'public fact_sales'[Transaction Type] = "Sale", FILTER(ALL('public dim_date'), 'public dim_date'[Date] < StartMonth))
VAR Result = COUNTROWS(EXCEPT(CurrentPeriod, PreviousCustomers))
RETURN IF(ISBLANK(Result), 0, Result)
```

**Retention Rate %**  
Shows the proportion of customers from a cohort who remained active.

```DAX
Retention Rate % = 
DIVIDE(SUM('public fact_cohort'[Active Customers]), SUM('public fact_cohort'[Cohort Size]))
```

**Filtered Customer Count**  
Returns the number of customers in the latest or currently selected month

Accounts for whether a filter is applied — defaults to the latest month if no filter. 
Helps prevent confusing blank visuals or misaligned data, 
especially when working with slicers. 
Uses IF(ISBLANK(...), fallback) pattern to provide consistent output.

```DAX
Filtered Customer Count = 
VAR SelectedMonth = MAX('public fact_customer_rfm_monthly'[Month Label])
VAR LatestMonth = CALCULATE(MAX('public fact_customer_rfm_monthly'[Month Label]), ALL('public fact_customer_rfm_monthly'))
VAR TargetMonth = IF(ISBLANK(SelectedMonth), LatestMonth, SelectedMonth)
RETURN CALCULATE(DISTINCTCOUNT('public fact_customer_rfm_monthly'[Customer ID]), 'public fact_customer_rfm_monthly'[Month Label] = TargetMonth)
```
